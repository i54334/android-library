buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
    }
}

repositories {
    jcenter()
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 17
        versionName "1.0.16"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        classpath += files(variant.javaCompile.classpath.files)
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = "com.github.axet"
archivesBaseName = "android-library"
version = android.defaultConfig.versionName

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Android Library'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Android Simple Widgets and Support classes.'
                url 'https://github.com/axet/android-library'

                scm {
                    connection 'scm:git:https://github.com/axet/android-library'
                    developerConnection 'scm:git:https://github.com/axet/android-library'
                    url 'https://github.com/axet/android-library'
                }

                licenses {
                    license {
                        name 'GNU LESSER GENERAL PUBLIC LICENSE 3.0'
                        url 'http://www.gnu.org/copyleft/lesser.html'
                    }
                }

                developers {
                    developer {
                        id 'axet'
                        name 'Alexey Kuznetsov'
                        email 'axet@me.com'
                    }
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:support-v4:23.2.1'
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.android.support:preference-v14:23.2.1'
}
